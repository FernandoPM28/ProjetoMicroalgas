#include <Arduino.h>
#include "include/AcquaShield.h"

ECShield Shield_EC; // Instancia a variável "ECShield".

void setup() 
{
    Serial.begin(9600);   // Inicializa a UART do Arduino.
    Shield_EC.init();     // Inicializa o Shield de EC.

    delay(1000);
    Shield_EC.DispBuffClear();                      // Limpa o buffer do display do Shield de EC.
    Shield_EC.DispWrite(3, 13, 21, "Acquanativa");  // Mostra "Acquanativa" no display.
    Shield_EC.DispShow();                           // Atualiza o display.
    delay(1000); 
}

void loop() 
{
    static unsigned long previous_mod = 0;  // Variável para contagem do tempo de loop.
    const unsigned long interval = 1000;    // Intervalo de atualização em milissegundos.

    if (millis() - previous_mod >= interval) 
    {
        previous_mod = millis();

        // Limpa o buffer do display do Shield de EC.
        Shield_EC.DispBuffClear();

        // Leitura de Condutividade Elétrica (EC).
        float ec = Shield_EC.EC();
        char ec_str[12];
        dtostrf(ec, 5, (ec < 100) ? 2 : (ec < 1000) ? 1 : 0, ec_str); // Ajusta casas decimais.
        Shield_EC.DispWrite(2, 0, 0, "EC (uS/cm):");
        Shield_EC.DispWrite(2, 80, 0, ec_str); // Mostra EC no display.

        // Leitura de Temperatura.
        float temp = Shield_EC.temp();
        char temp_str[6];
        dtostrf(temp, 2, 1, temp_str); // Converte temperatura para string.
        Shield_EC.DispWrite(1, 0, 10, "Temp (C):");
        Shield_EC.DispWrite(1, 70, 10, temp_str); // Mostra temperatura no display.

        // Leitura de TDS.
        float tds = Shield_EC.Tds();
        char tds_str[12];
        dtostrf(tds, 5, (tds < 100) ? 2 : (tds < 1000) ? 1 : 0, tds_str); // Ajusta casas decimais.
        Shield_EC.DispWrite(1, 0, 20, "TDS (ppm):");
        Shield_EC.DispWrite(1, 70, 20, tds_str); // Mostra TDS no display.

        // Leitura de Salinidade.
        float salin = Shield_EC.Salin();
        char salin_str[12];
        dtostrf(salin, 5, 2, salin_str); // Ajusta casas decimais.
        Shield_EC.DispWrite(1, 0, 30, "Salin (ppt):");
        Shield_EC.DispWrite(1, 70, 30, salin_str); // Mostra Salinidade no display.

        // Atualiza o display.
        Shield_EC.DispShow();

        // Envia os dados para o Serial Monitor.
        Serial.print("EC (uS/cm): ");
        Serial.print(ec);
        Serial.print(" | Temp (C): ");
        Serial.print(temp);
        Serial.print(" | TDS (ppm): ");
        Serial.print(tds);
        Serial.print(" | Salin (ppt): ");
        Serial.println(salin);
    }
}
